%token NUMBER STRING WORD COMMENT
%token JOSI JOSI_HA JOSI_TEN
%token DEF_FUNC DEF_CLASS DEF_CLASS_INIT
%token IF THEN ELSE LOOP NEW RET
%left '+' '-'
%left '*' '/' '%'
%left '&' '|'
%nonassoc UMINUS

%%
program : stmt_list { return lib_dic.commands['test'] + '\n' + $1; }
        ;

stmt_list : { $$ = '';}
		| stmt_list stmt { $$ = $1 + $2+ '\n';}
		;

stmt : assign { $$ = $1 + ';' }
		| funcall { $$ = $1 + ';' }
		| def_variable
		| def_function
		| def_class
		| def_class_init
		| if_stmt
		| loop_stmt
		| ret_stmt
		| COMMENT
		;

assign : variable '=' expr { $$ = $1 + '=' + $3;}
		| variable '+' '=' expr { $$ = $1  + '+=' + $4;}
		| variable '-' '=' expr { $$ = $1  + '-=' + $4;}
		| variable '*' '=' expr { $$ = $1  + '*=' + $4;}
		| variable '/' '=' expr { $$ = $1  + '/=' + $4;}
		;

funcall : WORD { $$ = lib_dic.get_comm($1,'');}
		| args WORD { $$ = lib_dic.get_comm($2, $1);}
		| variable JOSI_TEN WORD { $$ = $1 + '.' + lib_dic.get_method($1, $3, '');}
		| variable JOSI_TEN args WORD { $$ = $1 + '.' + lib_dic.get_method($1, $4, $3);}
		| new_stmt
		;

def_variable : variable {$$ = $1 + ';';}
		;

def_function : DEF_FUNC WORD '{' stmt_list '}'
			{$$ = 'void ' + lib_dic.get_comm($2,'') + '{\n' + $4 + '}';}
		|DEF_FUNC WORD JOSI_HA WORD '{' stmt_list '}'
			{$$ = lib_dic.get_types($4) + ' ' + lib_dic.get_comm($2,'') + '{\n' + $6 + '}';}
		|DEF_FUNC args WORD '{' stmt_list '}'
			{$$ = 'void ' + lib_dic.get_comm($3,$2) + '{\n' + $5 + '}';}
		|DEF_FUNC args WORD JOSI_HA WORD '{' stmt_list '}'
			{$$ = lib_dic.get_types($5) + ' ' + lib_dic.get_comm($3,$2) + '{\n' + $7 + '}';}
		;

def_class : DEF_CLASS WORD '{' stmt_list '}'
			{$$ = 'class ' + lib_dic.get_types($2) + '{\n' + $4 + '}';}
		;

def_class_init : DEF_CLASS_INIT WORD '{' stmt_list '}'
			{$$ = lib_dic.get_types($2) + '()' + ' {\n' + $4 + '}';}
		|DEF_CLASS_INIT args WORD '{' stmt_list '}'
			{$$ = lib_dic.get_types($3) + '(' + $2 + ') ' + '{\n' + $5 + '}';}
		;

new_stmt : NEW WORD { $$ = 'new ' + lib_dic.get_types($2) + '()'; }
		| NEW args WORD
			{$$ = 'new ' + lib_dic.get_types($3) + '(' + $2 + ')'; }
		| NEW WORD '[' expr ']' 
			{ $$ = 'new ' + lib_dic.get_types($2) + '[' + $4 + ']'; }
		;

if_stmt : IF '{' phrase_part '}' THEN '{' stmt_list '}'
			{$$ = lib_dic.get_comm($1,$3) + ' {\n' + $7 + '}'; }
		| IF '{' phrase_part '}' THEN '{' stmt_list '}' else_stmt
			{$$ = lib_dic.get_comm($1,$3) + ' {\n' + $7 + '} \n' + $9; }
		;

else_stmt : ELSE '{' stmt_list '}'
				{$$ = 'else {\n' + $3 +'}'; }
		| ELSE if_stmt
				{$$ = 'else ' + $2 ; }
		;

loop_stmt : '{' phrase_part '}' JOSI LOOP '{' stmt_list '}'
			{ $$ = lib_dic.get_comm($5,$2) + ' {\n' + $7 + '}'; }
		| '{' WORD ':' assign ',' WORD ':' expr ',' WORD ':' assign '}' JOSI LOOP '{' stmt_list '}'
			{ $$ = lib_dic.get_comm($15,$4 + ';' + $8 + ';' + $12) + ' {\n' + $17 + '}'; }
		;

ret_stmt : args RET {$$ = 'return ' + $1 + ';'; }
		;

args : phrase
		| args phrase { $$ = $1 + ',' + $2;}
		;

phrase : '{' phrase_part '}' JOSI { $$ = $2; }
		;

phrase_part : WORD ':' expr { $$ = $3; }
		| phrase_part ',' WORD ':' expr { $$ = $1 + ',' + $5; }
		;

expr : expr '+' expr { $$ = $1 + '+' + $3; }
		| expr '-' expr { $$ = $1 + '-' + $3; }
		| expr '*' expr { $$ = $1 + '*' + $3; }
		| expr '/' expr { $$ = $1 + '/' + $3; }
		| expr '%' expr { $$ = $1 + '%' + $3; }
		| expr '&' '&' expr { $$ = $1 + '&&' + $4; }
		| expr '|' '|' expr { $$ = $1 + '||' + $4; }
		| expr '<' expr { $$ = $1 + '<' + $3; }
		| expr '>' expr { $$ = $1 + '>' + $3; }
		| expr '<' '=' expr { $$ = $1 + '<=' + $4; }
		| expr '>' '=' expr { $$ = $1 + '>=' + $4; }
		| expr '!' '=' expr { $$ = $1 + '!=' + $4; }
		| expr '=' '=' expr { $$ = $1 + '==' + $4; }
		| '(' expr ')' { $$ = '(' + $2 + ')'; }
		| '-' expr %prec UMINUS {$$ = '-' + $2;} 
		| '!' expr {$$ = '!' + $2;} 
		| funcall
		| variable
		| primary
		;

variable : '@' WORD {$$ = lib_dic.get_vars($2);}
		| '$' WORD {$$ = lib_dic.get_vars($2);}
		| '@' WORD JOSI_HA WORD 
			{$$ = lib_dic.get_types($4) + " " + lib_dic.get_vars($2);
			 lib_dic.set_vars_type(lib_dic.get_vars($2), lib_dic.get_types($4));}
		| '@' WORD JOSI '@' WORD 
			{$$ = lib_dic.get_vars($2) + "." + lib_dic.get_property(lib_dic.get_vars($2), $5);}
		| '@' WORD '[' expr ']' {$$ = lib_dic.get_vars($2) + '[' + $4 + ']';}
		| '@' WORD '[' expr ']' JOSI '@' WORD 
			{$$ = lib_dic.get_vars($2) + '[' + $4 + ']' 
			 + "." + lib_dic.get_property(lib_dic.get_vars($2), $8);}
		| '@' WORD JOSI_HA WORD '[' ']' 
			{$$ = lib_dic.get_types($4) + '[] ' + lib_dic.get_vars($2);
			 lib_dic.set_vars_type(lib_dic.get_vars($2), lib_dic.get_types($4))}
		;

primary : NUMBER
		| STRING
		;

%%
